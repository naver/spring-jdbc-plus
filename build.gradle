import org.springframework.boot.gradle.plugin.SpringBootPlugin

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            url "https://repo.spring.io/milestone/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

allprojects {
    group = "com.navercorp.spring"
    version = "3.2.3-SNAPSHOT"
}

subprojects {
    apply plugin: "java"
    apply plugin: "java-library"
    apply plugin: "idea"
    apply plugin: "checkstyle"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "maven-publish"
    apply plugin: "signing"

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    repositories {
        mavenCentral()
        maven {
            url "https://repo.spring.io/milestone/"
        }
    }

    dependencies {
        compileOnly("com.google.code.findbugs:jsr305:3.0.2")
        testCompileOnly("com.google.code.findbugs:jsr305:3.0.2")
    }

    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
        }
    }

    checkstyle {
        configFile = file("${project.rootDir}/rule/naver-checkstyle-rules.xml")
        configProperties = ["suppressionFile": "${project.rootDir}/tool/naver-checkstyle-suppressions.xml"]
        toolVersion = "9.1"
        ignoreFailures = true // TODO: return to false
        maxErrors = 0
        maxWarnings = 0
    }

    test {
        useJUnitPlatform()
    }

    tasks.withType(Javadoc).all { enabled = false }
    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs << '-parameters'
    }

    java {
        withJavadocJar()
        withSourcesJar()
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                repositories {
                    maven {
                        def ossrhUsername = project.hasProperty("ossrhUsername") ? ossrhUsername : ""
                        def ossrhPassword = project.hasProperty("ossrhPassword") ? ossrhPassword : ""

                        credentials {
                            username ossrhUsername
                            password ossrhPassword
                        }

                        def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                        def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
                        url = version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
                    }
                }

                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }

                pom {
                    name = "Spring JDBC Plus"
                    description = "Spring JDBC Plus"
                    url = "http://github.com/naver/spring-jdbc-plus"
                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                    developers {
                        developer {
                            id = "mhyeon-lee"
                            name = "Myeonghyeon Lee"
                            email = "mheyon.lee@gmail.com"
                        }
                        developer {
                            id = "yd-2"
                            name = "Youngdae Lee"
                            email = "yd.2@navercorp.com"
                        }
                    }
                    scm {
                        connection = "scm:git:git://github.com/naver/spring-jdbc-plus.git"
                        developerConnection = "scm:git:git://github.com/naver/spring-jdbc-plus.git"
                        url = "https://github.com/naver/spring-jdbc-plus"
                    }
                }
            }
        }
    }


    signing {
        sign publishing.publications.mavenJava
    }

    tasks.withType(Sign) {
        onlyIf { !version.endsWith("SNAPSHOT") }
    }
}
