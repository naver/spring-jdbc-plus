import org.springframework.boot.gradle.plugin.SpringBootPlugin

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            url "https://repo.spring.io/milestone/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.2.6.RELEASE")
    }
}

allprojects {
    group = "com.navercorp.spring"
    version = "2.0.0.RC1"

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 60, "minutes"
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: "idea"
    apply plugin: "checkstyle"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "maven"
    apply plugin: "maven-publish"

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    repositories {
        mavenCentral()
        maven {
            url "https://repo.spring.io/milestone/"
        }
    }

    dependencies {
        compileOnly("com.google.code.findbugs:jsr305:3.0.2")
    }

    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
        }
    }

    checkstyle {
        configFile = file("${project.rootDir}/rule/naver-checkstyle-rules.xml")
        configProperties = [config_loc: ".."]
        toolVersion = "8.24"
    }

    test {
        useJUnitPlatform()
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allJava
        archiveClassifier = 'sources'
    }

    task javadocJar(type: Jar) {
        from javadoc
        archiveClassifier = 'javadoc'
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
            }
        }
        repositories {
            mavenCentral()
        }
    }
}
