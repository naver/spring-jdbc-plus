plugins {
    id "maven-publish"
    id "org.jetbrains.kotlin.jvm"
    id 'org.jreleaser'
}

group = "com.navercorp.spring"
version = "3.5.6-SNAPSHOT"

jreleaser {
    release {
        github {
            skipRelease = true
        }
    }
    gitRootSearch = true
    signing {
        active = 'ALWAYS'
        armored = true
        verify = true
    }
    deploy {
        maven {
            mavenCentral {
                'release-deploy' {
                    active = 'RELEASE'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    sign = true
                    stagingRepository('build/staging-deploy')
                }
            }
            // The following configuration is applied when the version matches the "*-SNAPSHOT" pattern.
            nexus2 {
                'snapshot-deploy' {
                    active = 'SNAPSHOT'
                    snapshotUrl = 'https://central.sonatype.com/repository/maven-snapshots/'
                    applyMavenCentralRules = true
                    snapshotSupported = true
                    closeRepository = true
                    releaseRepository = false
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            repositories {
                maven {
                    url = layout.buildDirectory.dir("staging-deploy")
                }
            }

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = "Spring JDBC Plus"
                description = "Spring JDBC Plus"
                url = "http://github.com/naver/spring-jdbc-plus"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "mhyeon-lee"
                        name = "Myeonghyeon Lee"
                        email = "mheyon.lee@gmail.com"
                    }
                    developer {
                        id = "yd-2"
                        name = "Youngdae Lee"
                        email = "yd.2@navercorp.com"
                    }
                    developer {
                        id = "chanhyeong"
                        name = "Chanhyeong Cho"
                        email = "chu7825@gmail.com"
                    }
                    developer {
                        id = "wool0826"
                        name = "Chanwool Jo"
                        email = "chanwool94@gmail.com"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/naver/spring-jdbc-plus.git"
                    developerConnection = "scm:git:git://github.com/naver/spring-jdbc-plus.git"
                    url = "https://github.com/naver/spring-jdbc-plus"
                }
            }
        }
    }
}
